plugins {
	id 'dev.architectury.loom' version '1.1-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.+'
	id 'maven-publish'
}

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_17

group = project.maven_group
version = "${project.mod_version}+${project.minecraft_version}"
archivesBaseName = project.mod_id

loom {
    forge {
        mixinConfigs = [
                "spruceui.mixins.json"
        ]
    }
}

repositories {
	mavenLocal()
}

configurations {
	api.extendsFrom libApi
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	forge "net.minecraftforge:forge:${project.forge_version}"

	modApi "dev.architectury:architectury-forge:${project.architectury_version}"

	libApi 'org.jetbrains:annotations:20.1.0'

}

java {
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = 'UTF-8'

	it.options.release.set(16)
}

processResources {
	inputs.property 'version', project.version

	filesMatching('META-INF/mods.toml') {
		expand 'version': project.version
	}
}

jar {
	from('LICENSE') {
		rename { "${it}_${project.archivesBaseName}" }
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java

			pom {
				name = 'SpruceUI'
				description = 'A Minecraft mod API which adds some GUI utilities.'
			}
		}
	}

	repositories {
		mavenLocal()
		maven {
			name 'BuildDirLocal'
			url "$buildDir/repo"
		}
	}
}
